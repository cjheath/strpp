// JSON grammar in Px. See http://json.org/ and ECMA-262 Ed.5
// Unicode uses 16-bit format, so Emoji's require a surrogate pair, see ISO/IEC 10646
// High surrogate first: D800 to DBFF, Low surrogate: D800 to DBFF
// https://www.ecma-international.org/wp-content/uploads/ECMA-404_2nd_edition_december_2017.pdf

s		= *[ \t\r\n]				// Zero or more space characters

TOP		= s (| object |array |string |'true' |'false' |'null' |number ) s

object		= '{' *(|(string ':' TOP *(',' string ':' TOP)) | s) '}'

array		= '[' (|(TOP *(',' TOP)) |s) ']'

string		= s ["] *(|[^"\\\u{0}-\u{1F}] |escape) ["] s

escape		= [\\] (|["\\/bfnrt] | 'u' [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] )

number		= ?'-' (|'0' | [1-9] *[0-9]) ?('.' +[0-9]) ?([eE] ?[-+] +[0-9])
